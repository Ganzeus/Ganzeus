

















<程序> →<变量说明部分>;<语句部分>
<变量说明部分> → <变量说明><标识符列表>
// <变量说明> → int
// <标识符列表> → <标识符列表>,<标识符>|<标识符>
<标识符列表> → <标识符><标识符列表p>
<标识符列表p> → ,<标识符><标识符列表p>|null

// <标识符> → $<字母>|<标识符><字母>|<标识符><数字>

// <语句部分> → <语句部分><语句>;|<语句>;
<语句部分> → <语句>;<语句部分p>
<语句部分p> → <语句>;<语句部分p>|null

<语句> → <赋值语句>|<条件语句>|<循环语句>
<赋值语句> → <标识符>=<表达式>

// <表达式> → <项>|<表达式><加法运算符><项>
<表达式> → <项><表达式p>
<表达式p> → <加法运算符><项><表达式p>|null

// <项> → <因子>|<项><乘法运算符><因子>
<项> → <因子><项p>
<项p> → <乘法运算符><因子><项p>|null

<因子> → <标识符>|<常量>|(<表达式>)
<常量> → <无符号整数>
<无符号整数> → <数字序列>
<数字序列> → <数字序列><数字>|<数字>
<加法运算符> → +
<乘法运算符> → *
<关系运算符> → <|>|!= |>=|<= |==
<条件> → <表达式><关系运算符><表达式>
<复合语句> → begin <语句部分> end
<条件语句> → if （<条件>） then <嵌套语句>; else <嵌套语句>
<循环语句> → while （<条件>） do <嵌套语句>
<嵌套语句> → <语句>|<复合语句>
<字母> → a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|x|y|z
<数字> → 0|1|2|3|4|5|6|7|8|9
 


int v, c;
v = 41;
c = 15;
c = v + 4*6 + (v*3)*c + 940 * (v*6);
if(v * 89 < v) then v = c * 479;
else begin c = c + 85;
    c = c + 734;
    end;
while(c * 6 > v) do v = v + 9;